import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

plugins {
    id 'java'
    id 'application'
    id 'sonarqube' version '3.3'
}

group 'de.hsmw'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

def javaFXPlatform = getJavaFXPlatform()
def javaFXVersion = "17-ea+11"

dependencies {
    implementation "org.openjfx:javafx-base:${javaFXVersion}:${javaFXPlatform}"
    implementation "org.openjfx:javafx-media:${javaFXVersion}:${javaFXPlatform}"
    implementation "org.openjfx:javafx-controls:${javaFXVersion}:${javaFXPlatform}"
    implementation "org.openjfx:javafx-fxml:${javaFXVersion}:${javaFXPlatform}"
    implementation "org.openjfx:javafx-graphics:${javaFXVersion}:${javaFXPlatform}"
    implementation 'org.apache.commons:commons-lang3:3.12.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

application {
    mainModule.set('de.hsmw.kriegZurSee')
    mainClass.set('de.hsmw.kriegZurSee.App')
}

sonarqube {
    properties {
    property "sonar.projectKey", "Vulnerius_Beleg"
    property "sonar.organization", "vulnerius"
    property "sonar.host.url", "https://sonarcloud.io"
  }
}
sourceSets {
    main {
        resources {
            srcDirs("src/resources")
        }
        output.resourcesDir = "build/classes/java/main"
    }

    test {
        output.resourcesDir = "build/classes/test"
    }
}

java {
    modularity.inferModulePath.set(true)
}

private static String getJavaFXPlatform() {
    def currentOS = DefaultNativePlatform.currentOperatingSystem;
    if (currentOS.isWindows()) {
        return 'win'
    } else if (currentOS.isLinux()) {
        return 'linux'
    } else if (currentOS.isMacOsX()) {
        return 'mac'
    }
    return null
}

test {
    useJUnitPlatform()
}